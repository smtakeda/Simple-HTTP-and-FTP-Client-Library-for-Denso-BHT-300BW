'##########################################################################
'
' Post Cache
'
'##########################################################################

'$include:'const.inc'
'$include:'util.def'
'$include:'util.inc'
'$include:'input.def'
'$include:'input.inc'
'$include:'screen.def'
'$include:'screen.inc'
'$include:'com.def'
'$include:'com.inc'
'$include:'postcach.def'

'-- shared const variable
  private PostCache.PostDataFileName$[20]

'------------------------------------------------------------
' queue post
'------------------------------------------------------------
  function PostCache.Queue%(barcode$,num$,id$,func%)
    private fileNum%:fileNum% = 4

    private funcStr$[1]
    funcStr$=mid$(str$(func%),2)
    private flen&

    '-- check file size
    open PostCache.PostDataFileName$ AS #fileNum% RECORD PostCache.RECORDLEN
    field #fileNum%, PostCache.BARCODELEN as barcode$, PostCache.NUMLEN as num$, PostCache.IDLEN as id$, PostCache.FUNCLEN as funcStr$
    flen&=lof(#fileNum%)
    close #fileNum%
    if flen&>=PostCache.RECORDLEN then
      private ret%
      ret%=PostCache.Flush% '-- flush
      flen&=0
    end if

    '-- add a request to queue
    open PostCache.PostDataFileName$ AS #fileNum% RECORD PostCache.RECORDLEN
    field #fileNum%, PostCache.BARCODELEN as barcode$, PostCache.NUMLEN as num$, PostCache.IDLEN as id$, PostCache.FUNCLEN as funcStr$
    put #fileNum%, flen&+1
    close #fileNum%
    PostCache.Queue%=0
  end function

'------------------------------------------------------------
' flush post
'------------------------------------------------------------
  function PostCache.Flush%
    private startTime$[8]
    private endTime$[8]
    startTime$ = time$ '-- start measuring

    private ret$[Com.RECVSIZE]

    private status%
    status%=Com.Status.Init '-- init status

    private fileNum%:fileNum% = 4
    private barcode$[PostCache.BARCODELEN]
    private num$[PostCache.NUMLEN]
    private id$[PostCache.IDLEN]
    private func$[PostCache.FUNCLEN]

    private flen&
    private idx&
    idx&=1

    open PostCache.PostDataFileName$ AS #fileNum% RECORD PostCache.RECORDLEN
    field #fileNum%, PostCache.BARCODELEN as barcode$, PostCache.NUMLEN as num$, PostCache.IDLEN as id$, PostCache.FUNCLEN as func$
    flen&=lof(#fileNum%)
    if flen&>0 then
      call Screen.Init
      call Screen.Footer
      locate 1,1
      print "»°ÊÞ°‚Æ“¯Šú‚µ‚Ä‚Ü‚·..."
      '-- state machine
      while status%<>Com.Status.DeviceClosed
        select status%
          case Com.Status.Init
            status%=Com.TcpIp.OpenDevice%(status%)
          case Com.Status.DeviceOpened
            status%=Com.TcpIp.Open%(status%)
          case Com.Status.Opened
            status%=Com.TcpIp.OpenSocket%(status%)
          case Com.Status.SocketConnected
            locate 1,3
            print idx&;
            print "/";
            print flen&
            print "      "
            get #fileNum%, idx&
            if id$="" then
              status%=Com.Http.SendData%(Com.Http.PostMode,Com.Http.URLPREFIX$+"/postItem.php","c="+barcode$+"&n="+num$+"&f="+func$,status%)
            else
              status%=Com.Http.SendData%(Com.Http.PostMode,Com.Http.URLPREFIX$+"/updateItem.php","c="+barcode$+"&n="+num$+"&id="+id$+"&f="+func$,status%)
            end if
          case Com.Status.DataSent
            status%=Com.Http.RecvData%(ret$,status%)
          case Com.Status.DataReceived
            idx&=idx&+1
            if idx&<=flen& then
              status%=Com.Status.SocketConnected
            else
              status%=Com.Status.DataReceivedEnd
            end if
          case Com.Status.DataReceivedEnd
            status%=Com.TcpIp.CloseSocket%(status%)
          case Com.Status.SocketClosed
            status%=Com.TcpIp.Close%(status%)
          case Com.Status.Closed
            status%=Com.TcpIp.CloseDevice%(status%)
        end select
      wend
      clfile #fileNum% '-- delete file
      call Screen.Init
      call Screen.Footer
      locate 1,1
      print "»°ÊÞ°‚Æ‚Ì“¯Šú‚ªI—¹‚µ‚Ü‚µ‚½"
    end if
    close #fileNum%

    PostCache.Flush% = 0

    endTime$=time$

    APP.ELAPSEDTIME% = Util.GetSeconds%(startTime$, endTime$)
    APP.DEBUGMSG$ = ""
  end function
'------------------------------------------------------------
' init
'------------------------------------------------------------
  sub PostCache.Init
    PostCache.PostDataFileName$="POST.DAT"
  end sub

